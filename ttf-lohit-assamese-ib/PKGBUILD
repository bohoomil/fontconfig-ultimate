# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-assamese-ib
_src_pkg=lohit-assamese-ttf
pkgver=2.5.3
pkgrel=5
depends=('fontconfig')
pkgdesc="Lohit Assamese font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('custom:OFL')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/o/lohit/${_src_pkg}-${pkgver}.tar.gz"
        40-lohit-assamese.conf
        66-lohit-assamese.conf
        90-tt-lohit-assamese.conf)
sha1sums=('c6db744b895bec13e8e29cafe91a37366b0418a9'
          'baae0cc8902bb5d516fd39248ea946cf7c4c57d3'
          '5ff2c7e3e8da1de242263f146811d7ef07fb7050'
          'e112688f0922a409a8071059aadc7f5ed82273ec')

package(){
  cd "${srcdir}"/"${_src_pkg}-${pkgver}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-lohit-assamese.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-assamese.conf
  install -m644 ../66-lohit-assamese.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-assamese.conf
  install -m644 ../90-tt-lohit-assamese.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-assamese.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-assamese.conf .
  ln -s ../conf.avail/66-lohit-assamese.conf .
  ln -s ../conf.avail/90-tt-lohit-assamese.conf .
}
