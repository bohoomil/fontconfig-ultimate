# Contributor: orumin <dev@orum.in>
# Contributor: bohoomil <@zoho.com>

pkgname=ttf-koruri-ibx
pkgver=20140904
pkgrel=1
pkgdesc="Japanese TrueType font obtained by mixing M+ and Open Sans (fixed base family name)."
arch=('any')
url="http://sourceforge.jp/projects/koruri"
license=('APACHE')
depends=('fontconfig')
groups=('infinality-bundle-fonts-extra')
conflicts=('ttf-koruri')
replaces=('ttf-koruri')
_mirror="jaist"
install=${pkgname}.install
source=(http://${_mirror}.dl.sourceforge.jp/koruri/61919/Koruri-${pkgver}.tar.xz
        40-koruri.conf
        90-tt-koruri.conf)
sha1sums=('0abe8b438b8d23183a235bada1ce4dffab2cfe55'
          '9fdc5ef0e988112ce19cc2b20b1bbc22ca13c901'
          'be5278f69144dd42a749249e6ab7ed4e0dffb48b')

package() {
  cd "${srcdir}/Koruri-${pkgver}"

  install -d "${pkgdir}/usr/share/fonts/${pkgname}"
  install -m644 *.ttf "${pkgdir}/usr/share/fonts/${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-koruri.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-koruri.conf
  install -m644 ../90-tt-koruri.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-koruri.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-koruri.conf .
  ln -s ../conf.avail/90-tt-koruri.conf .
}
