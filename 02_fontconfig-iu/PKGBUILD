# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <bohoomil@zoho.com>
# Contributor: Julien Humbert <julroy67@gmail.com>

pkgname=fontconfig-infinality-ultimate
_basename=fontconfig
pkgver=2.12.1
pkgrel=1
pkgdesc="A library for configuring and customizing font access, optimized for freetype2-infinality-ultimate."
url='http://www.fontconfig.org/release/'
license=('custom' 'MIT')
arch=(i686 x86_64)
groups=('infinality-bundle')
depends=('expat' 'freetype2-infinality-ultimate')
makedepends=('python2' 'docbook2x')
provides=('fontconfig=$pkgver' 'fontconfig-infinality' 'fontconfig-infinality-ultimate')
conflicts=('fontconfig' 'fontconfig-infinality' 'fontconfig-infinality-git' 'fontconfig-infinality-ultimate-git')
replaces=('fontconfig-iu-comp')
backup=('etc/fonts/fonts.conf'
        'etc/fonts/conf.avail.infinality/combi/30-metric-aliases-combi.conf'
        'etc/fonts/conf.avail.infinality/combi/37-repl-global-combi.conf'
        'etc/fonts/conf.avail.infinality/combi/60-latin-combi.conf'
        'etc/fonts/conf.avail.infinality/combi/65-non-latin-combi.conf'
        'etc/fonts/conf.avail.infinality/combi/66-aliases-wine-combi.conf'
        'etc/fonts/conf.avail.infinality/35-repl-custom.conf'
        'etc/fonts/conf.avail.infinality/38-repl-webfonts-custom.conf'
        'etc/fonts/conf.avail.infinality/97-selective-rendering-custom.conf')
install=fontconfig-ultimate.install
source=(http://www.fontconfig.org/release/${_basename}-${pkgver}.tar.bz2
        http://bohoomil.com/src/${_basename}-ultimate-git.tar.bz2
        fc-cache-ib.hook
	01-configure.patch
	02-configure.ac.patch
	03-Makefile.in.patch
	04-Makefile.conf.d.patch
	05-Makefile.am.in.patch)
sha1sums=('30d832b754fb10a3b70ebac750a38a0275438ad8'
          'ec9fbf0674e2598edb8c4c535b1d2425ac223720'
          'de59ec72e8517d0c95b91bd3401fb0c49f4eea60'
          'e1d553da760b214b858930a920fb328e2226e887'
          'dc987cc9480e9f8272c1d983c649387f90e1ffcc'
          '164c93498a2b873d28e51b670ac9a581d5597538'
          '5dbe4998551c344f77b0cdc51e956f6b077d9a47'
          '7646dd5d52b6b8429653f128007626fe353f71e8')


# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html
# http://getemoji.com/

prepare() {
	# copy fontconfig-ib patches & stuff
	cd "${_basename}-ultimate-git"
	
	cp -r conf.d.infinality "${srcdir}/${_basename}-${pkgver}"/conf.d.infinality
	
	# prepare src
	cd "${srcdir}/${_basename}-${pkgver}"
	
	patch -Np1 -i ../01-configure.patch
	patch -Np1 -i ../02-configure.ac.patch
	patch -Np1 -i ../03-Makefile.in.patch
	patch -Np1 -i ../04-Makefile.conf.d.patch
	patch -Np1 -i ../05-Makefile.am.in.patch
	
	aclocal
	libtoolize -f
	automake -afi
}

build() {
	cd $_basename-$pkgver
	./configure --prefix=/usr \
		--sysconfdir=/etc \
		--with-templatedir=/etc/fonts/conf.avail \
		--with-templateinfdir=/etc/fonts/conf.avail.infinality \
		--with-xmldir=/etc/fonts \
		--localstatedir=/var \
		--disable-static \
		--with-default-fonts=/usr/share/fonts \
		--with-add-fonts=/usr/share/fonts
	make
}

check() {
	cd $_basename-$pkgver
	make -k check
}

package() {
	cd $_basename-$pkgver
	make DESTDIR="$pkgdir" install
	
	# Install license
	install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
	
	# Alpm hook
	install -D -m 644 "${srcdir}"/fc-cache-ib.hook \
		"${pkgdir}/usr/share/libalpm/hooks/90-fc-cache-ib.hook"
	
	# Install infinality stuff
	cd "${srcdir}/${_basename}-ultimate-git"
	
	# Copy presets
	cp -r fontconfig_patches/{combi,free,ms} \
		"${pkgdir}"/etc/fonts/conf.avail.infinality
	
	# Install fc-presets
	install -m755 fontconfig_patches/"fc-presets" "${pkgdir}"/usr/bin/"fc-presets"
	
	# Copy documentation
	install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
	cp -r doc "${pkgdir}"/usr/share/
	
	find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
}
