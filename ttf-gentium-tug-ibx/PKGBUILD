# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-gentium-tug-ibx
pkgver=1.0
pkgrel=6
depends=('fontconfig')
pkgdesc="Gentium is a serif typeface family designed by Victor Gaultney."
url="http://www.ctan.org/tex-archive/fonts/gentium-tug"
arch=('any')
license=('custom:OFL')
groups=('infinality-bundle-fonts-extra')
conflicts=('ttf-gentium-basic' 'ttf-gentium-plus' 'ttf-gentium')
replaces=('ttf-gentium')
install=$pkgname.install
source=(http://mirrors.ctan.org/fonts/gentium-tug.zip
        45-gentium.conf
        90-tt-gentium.conf)
sha1sums=('66b6a3d4c03d9ac7057d4121b62c0fa678a734a4'
          '7df6e3c69c5edd63a2a6c380cdf053139dbd8884'
          'c5403887e6fe19137cb509c64e4c745a3fc3181c')

package(){
  cd "${srcdir}"/gentium-tug

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/OFL-FAQ.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 truetype/*.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-gentium.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-gentium.conf
  install -m644 ../90-tt-gentium.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-gentium.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-gentium.conf .
  ln -s ../conf.avail/90-tt-gentium.conf .
}
