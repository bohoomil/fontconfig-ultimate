# Maintainer: bohoomil <@zoho.com>

pkgname=otf-titillium-ibx
pkgver=2.0
pkgrel=5
depends=('fontconfig')
pkgdesc="Titillium is an elegant sans-serif typeface created at Accademia di Belle Arti di Urbino."
url="http://www.campivisivi.net/titillium/"
arch=('any')
groups=('infinality-bundle-fonts-extra')
conflicts=('otf-titillium')
replaces=('otf-titillium')
license=('custom:OFL')
source=(http://www.campivisivi.net/titillium/download/Titillium_roman_upright_italic_2_0_OT.zip
        31-titillium.conf
        45-titillium.conf
        90-non-tt-titillium.conf)
sha1sums=('f4dd29ce864df4c418b6c944e8747fe6b090f750'
          '7547c63937a717281c742b8d15cb562650674332'
          'a316fc8d82754b029e34aed3ef6fdaeb6be74c3a'
          '0811e599f694bcd74b484f4277df2911dff69437')

package() {
  cd "${srcdir}"/Titillium_roman_upright_italic_2_0_OT

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL-titillium.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING
  install -m644 OFL-FAQ.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/OFL-FAQ

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"

  family=(Titillium-Black.otf Titillium-{Bold,BoldItalic}.otf
          Titillium-{Light,LightItalic}.otf
          Titillium-{Regular,RegularItalic}.otf
          Titillium-{Semibold,SemiboldItalic}.otf
          Titillium-{Thin,ThinItalic}.otf)

  for font in "${family[@]}"; do
    install -m644 "$font" "${pkgdir}"/usr/share/fonts/"${pkgname}"
  done

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../31-titillium.conf \
    "${pkgdir}"/etc/fonts/conf.avail/31-titillium.conf
  install -m644 ../45-titillium.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-titillium.conf
  install -m644 ../90-non-tt-titillium.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-titillium.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/31-titillium.conf .
  ln -s ../conf.avail/45-titillium.conf .
  ln -s ../conf.avail/90-non-tt-titillium.conf .
}
