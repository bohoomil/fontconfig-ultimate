# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-consola-mono-ibx
pkgver=2.001
pkgrel=4
depends=('fontconfig')
pkgdesc="Consola Mono is a legible, programming font by Wojciech Kalinowski."
url="http://openfontlibrary.org"
arch=('any')
groups=('infinality-bundle-fonts-extra')
conflicts=('ttf-consolamono')
replaces=('ttf-consolamono')
license=('OFL-1.1')
install=$pkgname.install
source=("http://openfontlibrary.org/assets/downloads/consolamono/61cc2afcc4eca96efe7c6ebf178d39df/consolamono.zip"
        45-consola-mono.conf
        90-tt-consola-mono.conf)
sha1sums=('ba66ce10f62e9fee112631c6b2752fb68099fd76'
          'ba4bc92d00a222f8c35a1f4bd53515dea4626d5a'
          '5e6191875aba13d3eb57bcbdd3c632d75d758d77')

package(){
  cd "${srcdir}"/'Consola Mono'

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 "SIL - Open Font License.txt" \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 "${srcdir}"/45-consola-mono.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-consola-mono.conf
  install -m644 "${srcdir}"/90-tt-consola-mono.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-consola-mono.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-consola-mono.conf .
  ln -s ../conf.avail/90-tt-consola-mono.conf .
}
