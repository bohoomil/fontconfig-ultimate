# $Id: PKGBUILD 160629 2012-06-02 21:27:53Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>, bohoomil (mods)

# Installation order: freetype2, fontconfig, libxft, cairo
pkgname=libxft
pkgver=2.3.1
pkgrel=1
pkgdesc="FreeType-based font drawing library for X"
arch=('i686' 'x86_64')
license=('custom')
url="http://xorg.freedesktop.org/"
depends=('fontconfig' 'libxrender')
makedepends=('pkgconfig')
options=('!libtool')
source=("${url}/releases/individual/lib/libXft-${pkgver}.tar.bz2")
sha256sums=('7fce32b92dcb7b2869bed567af2abc7bbad0d5d6fcf471b8a3e137964a31bbbd')

build() {
  cd "${srcdir}/libXft-${pkgver}"
  ./configure --prefix=/usr \
		--sysconfdir=/etc \
		--disable-static
  make
}

package() {
  cd "${srcdir}/libXft-${pkgver}"
  make DESTDIR="${pkgdir}" install
  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"

}
