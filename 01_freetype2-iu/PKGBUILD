# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <@zoho.com>
# Contributor: Julien Humbert <julroy67@gmail.com>

pkgname=freetype2-infinality-ultimate
pkgver=2.7
pkgrel=1
pkgdesc="TrueType font rendering library with Infinality patches and custom settings."
arch=(i686 x86_64)
license=('GPL' 'MIT')
groups=('infinality-bundle')
url="http://www.freetype.org"
depends=('zlib' 'bzip2' 'sh' 'xorg-xrdb' 'libpng' 'harfbuzz')
conflicts=('freetype2' 'freetype2-infinality')
provides=("freetype2=$pkgver" 'freetype2-infinality' 'libfreetype.so')
install=infinality.install
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2
	http://download.savannah.gnu.org/releases/freetype/freetype-doc-${pkgver}.tar.bz2
	http://download.savannah.gnu.org/releases/freetype/ft2demos-${pkgver}.tar.bz2
	0001-Enable-table-validation-modules.patch
	0002-Enable-subpixel-rendering.patch
	0003-Enable-infinality-subpixel-hinting.patch
	0005-freetype-2.5.2-more-demos.patch
	freetype2.sh
	xft-settings.sh
	infinality-settings.sh
	infinality-settings-generic)
sha1sums=('f251029d5ab542a96dd4531605fe577185077560'
          '22949e7c809d7d5931331ca54da30e4d195669ea'
          'b7f7633ca0b828ca319afdd09a81b42592685d9c'
          '5ffd86fbf42aca32c6e4f0f4aa1e13c826f09a83'
          '2240060282082d1839239a0261e46734a28dfa4c'
          'c9626c80cc3f8621467c6236793176f8d1679720'
          'f869ae27c1c01c90218fbaa0b4f62f16a67a0da3'
          'f343d5b7c0636bf0136e06aab4fa69fe83b1be0e'
          'a1859f2eacae2046a9ef705ac2bcc4bdf4fd9717'
          '44ca94634f894011db9ed7edbb105b50ca591e52'
          '4d219670cb9641b649f6ba0f2a799006f7c3c3c5')

prepare() {
	# Rename source dir to allow building the demos
	mv freetype-${pkgver} freetype2
	
	cd freetype2
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
	
	cd ../ft2demos-${pkgver}
	# enable more demos
	patch -Np1 -i ../0005-freetype-2.5.2-more-demos.patch
	
	# Suppress RPATH
	sed -i '/X11_LIB:%=-R%/d' graph/x11/rules.mk
}

build() {
	cd freetype2
	./configure \
		--prefix=/usr \
		--disable-static \
		--with-harfbuzz \
		--with-png
	make
	
	# Build demos
	cd ../ft2demos-${pkgver}
	make
}

check() {
	cd freetype2
	make -k check
}

package() {
	cd freetype2
	make DESTDIR="${pkgdir}" install
	install -Dm644 ../freetype2.sh "${pkgdir}/etc/profile.d/freetype2.sh"
	
	# Package docs
	install -dm755 "${pkgdir}/usr/share/doc"
	cp -a docs "${pkgdir}/usr/share/doc/${pkgname}"
	
	# Package demos
	cd ../ft2demos-${pkgver}
	for _i in bin/{f,t}t*; do
		libtool --mode=install install $_i "$pkgdir/usr/bin/"
	done
	
	# freetype2 runtime settings
	install -m755 -d "${pkgdir}/etc/X11/xinit/xinitrc.d"
	install -m755 "${srcdir}/xft-settings.sh" \
		"${pkgdir}/etc/X11/xinit/xinitrc.d/xft-settings.sh"
	
	install -m755 -d "${pkgdir}/usr/share/doc/freetype2-infinality-ultimate"
	install -m755 "${srcdir}/infinality-settings.sh" \
		"${pkgdir}/usr/share/doc/freetype2-infinality-ultimate/infinality-settings.sh"
	install -m755 "${srcdir}/infinality-settings-generic" \
		"${pkgdir}/usr/share/doc/freetype2-infinality-ultimate/infinality-settings-generic"
}
