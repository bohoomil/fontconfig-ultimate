# Maintainer: bohoomil <@zoho.com>

pkgname=otf-inconsolatazi4-ibx
pkgver=1.04
pkgrel=6
depends=('fontconfig')
pkgdesc="Monospace font for pretty code listings and for the terminal."
url="http://www.levien.com/type/myfonts/inconsolata.html"
arch=('any')
license=('OFL-1.1')
groups=('infinality-bundle-fonts-extra')
replaces=('t1-inconsolata-zi4-ibx' 'otf-inconsolatazi4-ib')
conflicts=('t1-inconsolata-zi4' 't1-inconsolata-zi4-ibx' 'ttf-inconsolata' 'ttf-inconsolata-g' 'ttf-inconsolata-dz' 'otf-inconsolata' 'otf-inconsolatazi4-ib')
install=$pkgname.install
source=("http://mirrors.ctan.org/fonts/inconsolata.zip"
        45-inconsolatazi4.conf
        90-non-tt-inconsolatazi4.conf)
sha1sums=('33f60ff231e768aad05bd24537230a40f74e9cdb'
          '4d8d33210e76fb159bbef70946c3a25354b62a16'
          '9479c519abb5919e745b72a773d8bf4098e4b748')

package(){
  cd "${srcdir}"/inconsolata

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 doc/OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 opentype/*.otf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-inconsolatazi4.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-inconsolatazi4.conf
  install -m644 ../90-non-tt-inconsolatazi4.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-inconsolatazi4.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-inconsolatazi4.conf .
  ln -s ../conf.avail/90-non-tt-inconsolatazi4.conf .
}
