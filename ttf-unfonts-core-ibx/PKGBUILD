# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-unfonts-core-ibx
_basename=un-fonts-core
pkgver=1.0.2
_pkgrev=080608
pkgrel=6
depends=('fontconfig')
pkgdesc="Un Core family of Korean TrueType fonts."
url="http://kldp.net/projects/unfonts/"
arch=('any')
groups=('infinality-bundle-fonts-extra')
conflicts=('ttf-unfonts-core' 'ttf-unfontscore')
replaces=('ttf-unfonts-core' 'ttf-unfontscore')
license=('Custom')
install=$pkgname.install
source=(http://kldp.net/frs/download.php/4695/$_basename-$pkgver-$_pkgrev.tar.gz
        40-unfonts-core.conf
        90-tt-unfonts-core.conf)
sha1sums=('4245d08649a0acc26d8c73e707b61371b9370e7a'
          '17fd5aeff0a6f61c2a4e30b08327762600c9cc05'
          'bc9f39c51b4ca56cdd8167ff351c11d37a243ff0')

package(){
  cd "${srcdir}"/un-fonts

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 COPYING "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m644 README "${pkgdir}"/usr/share/doc/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-unfonts-core.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-unfonts-core.conf
  install -m644 ../90-tt-unfonts-core.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-unfonts-core.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-unfonts-core.conf .
  ln -s ../conf.avail/90-tt-unfonts-core.conf .
}
