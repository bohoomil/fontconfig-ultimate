# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-odia-ib
_src_pkg=lohit-odia-ttf
pkgver=2.5.5
pkgrel=5
depends=('fontconfig')
pkgdesc="Lohit Oriya font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts' 'ttf-lohit-oriya-ib')
replaces=('ttf-lohit-oriya-ib')
provides=('ttf-lohit-oriya-ib')
groups=('infinality-bundle-fonts')
license=('custom:OFL')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/o/lohit/${_src_pkg}-${pkgver}.tar.gz"
        40-lohit-odia.conf
        66-lohit-odia.conf
        90-tt-lohit-odia.conf)
sha1sums=('084a9de1c60f145dd9cb8b7bca99c812eefadc90'
          '31d2937b8487246d75380ba80ed89fcb67ff620a'
          'e0c47d862e55262c06cccb3916f31a9ee1869380'
          'e9c2bec9f3c8d2ef7a08c5e3947a8fce7247a38a')

package(){
  cd "${srcdir}"/"${_src_pkg}-${pkgver}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-lohit-odia.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-odia.conf
  install -m644 ../66-lohit-odia.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-odia.conf
  install -m644 ../90-tt-lohit-odia.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-odia.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-odia.conf .
  ln -s ../conf.avail/66-lohit-odia.conf .
  ln -s ../conf.avail/90-tt-lohit-odia.conf .
}
