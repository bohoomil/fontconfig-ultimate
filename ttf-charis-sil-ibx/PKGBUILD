# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-charis-sil-ibx
_src_name=CharisSIL
pkgver=4.114
pkgrel=3
depends=('fontconfig')
pkgdesc="Charis SIL is a serif typeface providing a comprehensive inventory of glyphs for almost any Roman- or Cyrilic-based writing system."
url="http://www.sil.org/"
arch=('any')
license=('custom:OFL')
conflicts=('ttf-charis')
groups=('infinality-bundle-fonts-extra')
install=$pkgname.install
source=(http://bohoomil.com/src/${_src_name}-${pkgver}.zip{,.sig}
        45-charis-sil.conf
        90-tt-charis-sil.conf)
validgpgkeys=('A9244FB5E93F11F0E975337FAE6866C7962DDE58')
sha1sums=('d0fbd177612ff843645af8b9755118251b89598b'
          'SKIP'
          '415235a6b837bb94f0aeec3ec21b040782020076'
          'dc3ba28b19a6cd9bc4010dab21d71ea99c0a60c7')

package(){
  cd "${srcdir}/${_src_name}-${pkgver}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m644 documentation/CharisSIL-features.pdf \
    "${pkgdir}"/usr/share/doc/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-charis-sil.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-charis-sil.conf
  install -m644 ../90-tt-charis-sil.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-charis-sil.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-charis-sil.conf .
  ln -s ../conf.avail/90-tt-charis-sil.conf .
}
