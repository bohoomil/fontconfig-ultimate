# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: bohoomil <bohoomil@zoho.com>

pkgname=fontconfig-infinality-ultimate
_basename=fontconfig
_gitname=fontconfig-ultimate
pkgdesc="A library for configuring and customizing font access (includes generic fontconfig package and ultimate settings by bohoomil)."
url=('http://www.fontconfig.org/release/')
license=('custom' 'CC-BY-SA')
pkgver=2.11.0
pkgrel=8
arch=('i686' 'x86_64')
groups=('infinality-bundle')
depends=('expat' 'freetype2-infinality-ultimate')
options=('!libtool')
provides=('fontconfig=$pkgver' 'fontconfig-infinality' 'fontconfig-infinality-ultimate')
conflicts=('fontconfig' 'fontconfig-infinality' 'fontconfig-infinality-git' 'fontconfig-infinality-ultimate-git')
backup=('etc/fonts/fonts.conf'
        'etc/fonts/conf.avail.infinality/custom/37-repl-global-custom.conf'
        'etc/fonts/conf.avail.infinality/custom/60-latin-custom.conf'
        'etc/fonts/conf.avail.infinality/custom/65-non-latin-custom.conf'
        'etc/fonts/conf.avail.infinality/custom/66-aliases-wine-custom.conf'
        'etc/fonts/conf.avail.infinality/custom/93-final-lang-spec-custom.conf')
install=fontconfig-ultimate.install
source=(http://www.fontconfig.org/release/${_basename}-${pkgver}.tar.bz2
        git://github.com/bohoomil/fontconfig-ultimate.git#branch=master)

# a nice page to test font matching:
# http://zipcon.net/~swhite/docs/computers/browsers/fonttest.html

#version() {
#  cd $_gitname
#  git log -1 --format="%cd" --date=short | sed 's|-|.|g'
#}

prepare() {
  patches=(00-fonts.conf.in.patch
           01-configure.patch
           02-configure.ac.patch
           03-Makefile.in.patch
           04-Makefile.conf.d.patch
           05-Makefile.am.in.patch)

  # copy fontconfig-ib patches & stuff
  cd "${srcdir}/${_gitname}"

  cp -r conf.d.infinality "${srcdir}/${_basename}-${pkgver}"/conf.d.infinality
  cp -r fontconfig_patches/*.patch "${srcdir}/${_basename}-${pkgver}"

  # prepare src
  cd "${srcdir}/${_basename}-${pkgver}"

  # infinality & post release fixes
  for patch in "${patches[@]}"; do
    patch -Np1 -i ${patch}
  done

  # make sure there's no rpath trouble and sane .so versioning -
  # FC and Gentoo do this as well
  aclocal
  libtoolize -f
  automake --add-missing -fi
}

build() {
  cd "${srcdir}/${_basename}-${pkgver}"

  ./configure --prefix=/usr \
    --sysconfdir=/etc \
    --with-templatedir=/etc/fonts/conf.avail \
    --with-templateinfdir=/etc/fonts/conf.avail.infinality \
    --with-xmldir=/etc/fonts \
    --localstatedir=/var \
    --disable-static \
    --with-default-fonts=/usr/share/fonts \
    --with-add-fonts=/usr/share/fonts
  make
}

check() {
  cd "${srcdir}/${_basename}-${pkgver}"
  make -k check
}

package() {
  cd "${srcdir}/${_basename}-${pkgver}"
  make DESTDIR="${pkgdir}" install

  #Install license
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${_basename}"
  install -m644 COPYING "${pkgdir}"/usr/share/licenses/"${_basename}"

  #install infinality stuff
  cd "${srcdir}/${_gitname}"

  # copy presets
  cp -r fontconfig_patches/{custom,free,ms} \
    "${pkgdir}"/etc/fonts/conf.avail.infinality

  # install fc-presets
  install -m755 fontconfig_patches/"fc-presets" "${pkgdir}"/usr/bin/"fc-presets"

  # install license
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/"${pkgname}"/LICENSE

  # copy documentation
  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  cp -r doc "${pkgdir}"/usr/share/

  # install infinality-settings.sh
  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"/freetype
  install -m755 freetype/infinality-settings.sh \
    "${pkgdir}"/usr/share/doc/"${pkgname}"/freetype/infinality-settings.sh

  find "${pkgdir}" -type d -name .git -exec rm -r '{}' +
}

md5sums=('000bd4baf7aefa828e03414d0c8c7dc5'
         'SKIP')
