# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-source-code-pro-ibx
pkgver=2.010ro+1.030it
_relver=2.010R-ro/1.030R-it
_tarver=${_relver//\//-}
pkgrel=2
depends=('fontconfig')
pkgdesc="A set of OpenType fonts designed for user interfaces. TrueType version."
url="http://www.adobe.com/"
arch=('any')
license=('custom:OFL')
conflicts=('adobe-source-sans-pro-fonts' 'otf-source-sans-pro' 'ttf-source-sans-pro' 't1-source-code-pro-ibx' 'otf-source-code-pro-ibx')
groups=('infinality-bundle-fonts-extra')
source=("${_tarver}.tar.gz::https://github.com/adobe-fonts/source-code-pro/archive/${_relver}.tar.gz"
        45-source-code-pro.conf
        90-tt-source-code-pro.conf)
sha1sums=('d266fb8cc558a85d76432ee0731144fade7b4be1'
          'd20feb494116f95e4a35ad40f625a94f9a3bbcb2'
          'ae4fa14a8aa927e325a8bfd7c0f8db29eefb00ab')

package(){
  cd "source-code-pro-$_tarver"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 LICENSE.txt \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 TTF/*.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  cd "${srcdir}"
  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-source-code-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-source-code-pro.conf
  install -m644 90-tt-source-code-pro.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-source-code-pro.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-source-code-pro.conf .
  ln -s ../conf.avail/90-tt-source-code-pro.conf .
}
