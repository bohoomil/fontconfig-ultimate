# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-devanagari-ib
_src_pkg=lohit-devanagari-ttf
pkgver=2.94.0
pkgrel=4
depends=('fontconfig')
pkgdesc="Lohit Devanagari font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('OFL-1.1')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/o/lohit/${_src_pkg}-${pkgver}.tar.gz"
        40-lohit-devanagari.conf
        66-lohit-devanagari.conf
        90-tt-lohit-devanagari.conf)
sha1sums=('4804f810215d60ec5334b7192ee965f39bfdac38'
          'ec03746a621de6456c0a412f392f762a3bea92e1'
          '2682411eefea9016663af2028196c22f89a18513'
          '3bd004a6eefef9eae20dbf2d2cb2d65e816ead30')

package(){
  cd "${srcdir}"/"${_src_pkg}-${pkgver}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-lohit-devanagari.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-devanagari.conf
  install -m644 ../66-lohit-devanagari.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-devanagari.conf
  install -m644 ../90-tt-lohit-devanagari.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-devanagari.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-devanagari.conf .
  ln -s ../conf.avail/66-lohit-devanagari.conf .
  ln -s ../conf.avail/90-tt-lohit-devanagari.conf .
}
