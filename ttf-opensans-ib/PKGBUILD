# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-opensans-ib
pkgver=1.2
pkgrel=16
depends=('fontconfig')
pkgdesc="Open Sans is a humanist sans serif typeface designed by Steve Matteson, Type Director of Ascender Corp."
url="http://opensans.com/"
arch=('any')
license=('Apache')
conflicts=('t1-opensans-ib' 'ttf-opensans')
provides=('ttf-opensans')
groups=('infinality-bundle-fonts')
install=$pkgname.install
source=(http://bohoomil.com/src/open_sans.zip
        http://bohoomil.com/src/open_sans_condensed.zip
        45-opensans.conf
        90-tt-opensans.conf)
sha1sums=('1fc651d6dd131baf795dd4b0dcc259980bcb9c58'
          'db8b311e0f139ca3ce5e1108a2d8b430af39e759'
          'e2cccbc4c5d9552430fe158351de374e2aa3b889'
          'c270db279deb3cf53b79e846dbb296f7d14c6322')

package(){
  cd "${srcdir}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 'LICENSE.txt' \
    "${pkgdir}"/usr/share/licenses/"${pkgname}"/COPYING

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -dm755 "${pkgdir}"/etc/fonts/conf.avail
  install -dm755 "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-opensans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-opensans.conf
  install -m644 90-tt-opensans.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-opensans.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-opensans.conf .
  ln -s ../conf.avail/90-tt-opensans.conf .
}

