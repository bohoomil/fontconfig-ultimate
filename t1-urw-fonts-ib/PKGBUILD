# Contributor: Firmicus <francois.archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Maintainer: bohoomil <@zoho.com>

pkgname=t1-urw-fonts-ib
pkgname_org=urw-fonts
pkgver=2.4
_fedoraver=2.4
_fedorarel=17.fc20
pkgrel=5
pkgdesc='Standard Ghostscript Type1 fonts from URW for infinality-bundle-fonts.'
url='http://sourceforge.net/projects/gs-fonts/'
arch=('any')
license=('GPL2')
makedepends=('rpmextract')
depends=('fontconfig')
conflicts=('gsfonts')
provides=('gsfonts' "$pkgname_org")
groups=('infinality-bundle-fonts')
install=$pkgname.install
source=("http://kojipkgs.fedoraproject.org/packages/${pkgname_org}/${_fedoraver}/${_fedorarel}/src/${pkgname_org}-${_fedoraver}-${_fedorarel}.src.rpm"
        45-urw-fonts.conf
        90-non-tt-urw-fonts.conf)
sha1sums=('0066723d9cd086cfdaecc57f7b216545a50924dd'
          '05ec45dce4b438527395c8be1528dd53580537cb'
          '9a5c09d0486e5feb498b4f357a69114ba9f4f3ef')

build() {
  cd "${srcdir}"
  rpmextract.sh "${pkgname_org}-${_fedoraver}-${_fedorarel}.src.rpm"
  bsdtar xf *.tar.bz2
}

package() {
  cd "${srcdir}"

  install -d "${pkgdir}"/usr/share/fonts/${pkgname}
  install -m644 *.{pfb,pfm,afm} "${pkgdir}"/usr/share/fonts/${pkgname}

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 45-urw-fonts.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-urw-fonts.conf
  install -m644 90-non-tt-urw-fonts.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-urw-fonts.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-urw-fonts.conf .
  ln -s ../conf.avail/90-non-tt-urw-fonts.conf .
}
