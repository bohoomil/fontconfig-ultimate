# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-kannada-ib
_src_pkg=lohit-kannada-ttf
pkgver=2.5.3
pkgrel=4
depends=('fontconfig')
pkgdesc="Lohit Kannada font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('OFL-1.1')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/o/lohit/${_src_pkg}-${pkgver}.tar.gz"
        40-lohit-kannada.conf
        66-lohit-kannada.conf
        90-tt-lohit-kannada.conf)
sha1sums=('5268501780e7732b75338bd9acba1b87e3e1ed70'
          '3e21bdbd863b6bcbecfb6e1657d74161240dfd11'
          '8cb492764c8bdedbcab030d2a4845ca617f41afa'
          'bcaa420c8b3d6fd9e1e9104820e7d54f6ad35dab')

package(){
  cd "${srcdir}"/"${_src_pkg}-${pkgver}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-lohit-kannada.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-kannada.conf
  install -m644 ../66-lohit-kannada.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-kannada.conf
  install -m644 ../90-tt-lohit-kannada.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-kannada.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-kannada.conf .
  ln -s ../conf.avail/66-lohit-kannada.conf .
  ln -s ../conf.avail/90-tt-lohit-kannada.conf .
}
