# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lohit-gurmukhi-ib
_src_pkg=lohit-gurmukhi-ttf
pkgver=2.91.0
pkgrel=5
depends=('fontconfig')
pkgdesc="Lohit Gurmukhi font."
url="https://fedorahosted.org/lohit/"
arch=('any')
conflicts=('lohit-fonts')
groups=('infinality-bundle-fonts')
license=('custom:OFL')
install=$pkgname.install
source=("https://fedorahosted.org/releases/l/o/lohit/${_src_pkg}-${pkgver}.tar.gz"
        40-lohit-gurmukhi.conf
        66-lohit-gurmukhi.conf
        90-tt-lohit-gurmukhi.conf)
sha1sums=('7e1fdcde744d779dfa792cfcd880c608a5d1f026'
          '987978152b954a4b5892089e7f53886fae21353d'
          'b30ae976b91f081de5363ccb321c75bac863fef4'
          '72f6a1e2cc68b61e11174d366fe7d762c554cd4c')

package(){
  cd "${srcdir}"/"${_src_pkg}-${pkgver}"

  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttf "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-lohit-gurmukhi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-lohit-gurmukhi.conf
  install -m644 ../66-lohit-gurmukhi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/66-lohit-gurmukhi.conf
  install -m644 ../90-tt-lohit-gurmukhi.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lohit-gurmukhi.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-lohit-gurmukhi.conf .
  ln -s ../conf.avail/66-lohit-gurmukhi.conf .
  ln -s ../conf.avail/90-tt-lohit-gurmukhi.conf .
}
