# Maintainer: bohoomil <@zoho.com>
# Contributor: Julien Humbert <julroy67@gmail.com>

_pkgbasename=freetype2
pkgname=lib32-$_pkgbasename-infinality-ultimate
pkgver=2.7
pkgrel=1
pkgdesc="TrueType font rendering library with Infinality patches and custom settings by bohoomil (32-bit, infinality-bundle)."
arch=(x86_64)
license=('GPL' 'MIT')
url="http://freetype.sourceforge.net"
groups=('infinality-bundle-multilib')
depends=('lib32-zlib' 'lib32-bzip2' 'lib32-libpng' 'lib32-harfbuzz' $_pkgbasename-infinality-ultimate)
makedepends=(gcc-multilib)
conflicts=('lib32-freetype2' 'lib32-freetype2-infinality' 'lib32-freetype2-git-infinality'
           'lib32-freetype2-ubuntu')
provides=('lib32-freetype2' 'lib32-freetype2-infinality')
source=(http://download.savannah.gnu.org/releases/freetype/freetype-${pkgver}.tar.bz2
	0001-Enable-table-validation-modules.patch
	0002-Enable-subpixel-rendering.patch
	0003-Enable-infinality-subpixel-hinting.patch)
sha1sums=('f251029d5ab542a96dd4531605fe577185077560'
          '5ffd86fbf42aca32c6e4f0f4aa1e13c826f09a83'
          '2240060282082d1839239a0261e46734a28dfa4c'
          'c9626c80cc3f8621467c6236793176f8d1679720')

prepare() {
	# Rename source dir to allow building the demos
	mv freetype-${pkgver} freetype2
	
	cd freetype2
	patch -Np1 -i ../0001-Enable-table-validation-modules.patch
	patch -Np1 -i ../0002-Enable-subpixel-rendering.patch
	patch -Np1 -i ../0003-Enable-infinality-subpixel-hinting.patch
}

build() {
	export CC="gcc -m32"
	export CXX="g++ -m32"
	export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
	
	cd freetype2
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib32 \
		--disable-static \
		--with-harfbuzz \
		--with-png
	make
}

check() {
	cd freetype2
	make -k check
}

package() {
	cd freetype2
	make DESTDIR="${pkgdir}" install
	
	rm -rf "${pkgdir}"/usr/{include,share,bin}
}
