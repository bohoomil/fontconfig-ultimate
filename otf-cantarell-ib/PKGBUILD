# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: bohoomil <@zoho.com>

pkgname=otf-cantarell-ib
_basename=cantarell-fonts
pkgver=0.0.25
pkgrel=2
epoch=1
pkgdesc="Cantarell font family."
arch=('any')
url="http://abattis.org/cantarell/"
license=('GPL2')
depends=('fontconfig')
conflicts=('t1-cantarell-ib' 'cantarell-fonts')
replaces=('cantarell-fonts')
provides=('cantarell-fonts')
source=(https://download.gnome.org/sources/${_basename}/${pkgver:0:3}/${_basename}-${pkgver}.tar.xz
        45-cantarell.conf
        90-non-tt-cantarell.conf)
sha1sums=('b893687a68efa3139b8b429560705b48ba118396'
          'c2fc52ab73d8b543bdf0d5b178e9e1b0fbdf3503'
          'f77dcf036ee0d1a46eb4dcc46cdbd4f350c875f0')

build() {
  cd "${_basename}-${pkgver}"
  ./configure --prefix=/usr \
    --with-fontdir=/usr/share/fonts/$pkgname \
    --with-configdir=/etc/fonts/conf.avail
  make
}

package() {
  cd "${_basename}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-cantarell.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-cantarell.conf
  install -m644 ../90-non-tt-cantarell.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-cantarell.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/31-cantarell.conf .
  ln -s ../conf.avail/45-cantarell.conf .
  ln -s ../conf.avail/90-non-tt-cantarell.conf .
}
