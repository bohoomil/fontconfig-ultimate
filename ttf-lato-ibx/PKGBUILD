# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-lato-ibx
pkgver=2.010
pkgrel=1
depends=('fontconfig')
pkgdesc="Lato is a sanserif type­face family designed in the Summer 2010 by Warsaw-based designer Łukasz Dziedzic."
url="http://www.latofonts.com/"
arch=('any')
groups=('infinality-bundle-fonts-extra')
license=('OFL-1.1')
conflicts=('ttf-lato' 't1-lato-ibx' 'otf-lato-ib')
replaces=('t1-lato-ibx' 'otf-lato-ib')
install=$pkgname.install
source=("http://www.latofonts.com/download/Lato2OFL.zip"
        lato-thin-${pkgver}-fixed.tar.xz
        45-lato.conf
        90-tt-lato.conf)
sha1sums=('875d4afb6f0f5748a5b82d6e74c3e8306e552609'
          'c3a88dfa01cace9440ec692905f6c1c956ce78d7'
          '86f9ff2da81ba10f64e6747fdd8d27231da28d2b'
          'a84058cb3051d858221a7a0eefeea18db24306c5')

package(){
  cd "${srcdir}"/Lato2OFL

  install -m755 -d "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m644 README.txt "${pkgdir}"/usr/share/doc/"${pkgname}"
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 OFL.txt "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"

lato1=(Lato-Black.ttf Lato-BlackItalic.ttf
       Lato-Bold.ttf Lato-BoldItalic.ttf
       Lato-Heavy.ttf Lato-HeavyItalic.ttf
       Lato-Regular.ttf Lato-Italic.ttf
       Lato-Light.ttf Lato-LightItalic.ttf
       Lato-Medium.ttf Lato-MediumItalic.ttf
       Lato-Semibold.ttf Lato-SemiboldItalic.ttf)

  for fnt in "${lato1[@]}"; do
    install -m644 "$fnt" "${pkgdir}"/usr/share/fonts/"${pkgname}"
  done

  # fixed weights
  install -m644 "${srcdir}"/lato-thin-fixed/*.ttf \
    "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../45-lato.conf \
    "${pkgdir}"/etc/fonts/conf.avail/45-lato.conf
  install -m644 ../90-tt-lato.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-tt-lato.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/45-lato.conf .
  ln -s ../conf.avail/90-tt-lato.conf .
}
