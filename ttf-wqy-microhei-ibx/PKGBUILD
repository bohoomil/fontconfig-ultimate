# Maintainer: Felix Yan <felixonmars@gmail.com>
# Contributor: Philacorns <opuspace@gmail.com>
# Maintainer: bohoomil <@zoho.com>

pkgname=ttf-wqy-microhei-ibx
_src_pkg=wqy-microhei
pkgver=0.2.0_beta
pkgrel=10
pkgdesc="A Sans-Serif style high quality CJK outline font."
arch=('any')
license=('GPL3' 'APACHE' 'custom:"font embedding exception"')
groups=('infinality-bundle-fonts-extra')
install=${pkgname}.install
url="http://wenq.org"
depends=('fontconfig')
conflicts=('wqy-microhei')
replaces=('wqy-microhei')
source=("http://downloads.sourceforge.net/project/wqy/${_src_pkg}/${pkgver//_/-}/${_src_pkg}-${pkgver//_/-}.tar.gz"
        40-wqy-microhei.conf
        90-non-tt-wqy-microhei.conf)
sha1sums=('28023041b22b6368bcfae076de68109b81e77976'
          '0468e9436df9e367901f373882ba92e6d842f714'
          '89549d2364edb12116fae566898213bff569e5cb')

build() {
  cd "${srcdir}/${_src_pkg}"
  tail README.txt --lines=16|head --lines=-3>| FONTEXCEPTION
}

package() {
  cd "${srcdir}/${_src_pkg}"
  install -m755 -d "${pkgdir}"/usr/share/licenses/"${pkgname}"
  install -m644 FONTEXCEPTION "${pkgdir}"/usr/share/licenses/"${pkgname}"

  install -m755 -d "${pkgdir}"/usr/share/fonts/"${pkgname}"
  install -m644 *.ttc "${pkgdir}"/usr/share/fonts/"${pkgname}"

  install -m755 -d "${pkgdir}"/etc/fonts/conf.avail
  install -m755 -d "${pkgdir}"/etc/fonts/conf.d
  install -m644 ../40-wqy-microhei.conf \
    "${pkgdir}"/etc/fonts/conf.avail/40-wqy-microhei.conf
  install -m644 ../90-non-tt-wqy-microhei.conf \
    "${pkgdir}"/etc/fonts/conf.avail/90-non-tt-wqy-microhei.conf

  cd "${pkgdir}"/etc/fonts/conf.d
  ln -s ../conf.avail/40-wqy-microhei.conf .
  ln -s ../conf.avail/90-non-tt-wqy-microhei.conf .
}
