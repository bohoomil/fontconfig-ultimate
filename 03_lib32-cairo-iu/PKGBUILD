# Contributor: Biru Ionut <biru.ionut at gmail.com>
# Contributor: Paul Bredbury <brebs@sent.com>
# Maintainer: Andrea Fagiani <andfagiani {at} gmail {dot} com>
# Maintainer: bohoomil <@zoho.com>

_pkgbasename=cairo
pkgname=lib32-$_pkgbasename-infinality-ultimate
pkgver=1.12.16
pkgrel=8
groups=('infinality-bundle')
pkgdesc="Cairo vector graphics library with respect-fontconfig and Ubuntu patches (32-bit, infinality-bundle)"
arch=('x86_64')
url="https://launchpad.net/ubuntu/precise/+source/cairo"
groups=('infinality-bundle-multilib')
license=('LGPL' 'MPL')
makedepends=('gcc-multilib' 'lib32-gtk2' 'lib32-mesa-libgl')
depends=('lib32-libpng' 'lib32-libxext' 'lib32-libxrender'
         'lib32-fontconfig-infinality-ultimate'  'lib32-pixman'
         'lib32-glib2' 'lib32-mesa' 'lib32-libgl'
         $_pkgbasename-infinality-ultimate)
provides=("lib32-cairo=$pkgver")
conflicts=('lib32-cairo' 'lib32-cairo-ubuntu')
options=('!libtool')
source=(http://cairographics.org/releases/$_pkgbasename-$pkgver.tar.xz
        cairo-1.12.16-lto-optional.patch
        cairo-respect-fontconfig_pb.patch
        http://archive.ubuntu.com/ubuntu/pool/main/c/cairo/cairo_1.12.16-0ubuntu2.debian.tar.gz)
sha1sums=('4f6e337d5d3edd7ea79d1426f575331552b003ec'
          'bbc22db3e7dde6522fbc916bf8503d3cafffb8b7'
          'ea6e6216e9aeeef0e692be63385f8bbb8aee21ca'
          '95d873fda436a1cc29cda1310f155ef0e3258c05')

prepare() {
  cd ${_pkgbasename}-${pkgver}

  #for _f in $(cat $srcdir/debian/patches/series) ; do
  #  patch -Np1 -i $srcdir/debian/patches/$_f
  #done

  patch -Np1 -i $srcdir/cairo-1.12.16-lto-optional.patch
  patch -Np1 -i $srcdir/cairo-respect-fontconfig_pb.patch

  patches=(01_build_perf_utils.patch
           02_am-maintainer-mode.patch
           03_export-symbols.patch
           06_hurd-map-noreserve.patch
           pdf-output-avoid-transparency.patch
           pdf-output-mime-data-embedding.patch
           server_side_gradients.patch)

  for patch in "${patches[@]}"; do
    patch -Np1 -i "${srcdir}"/debian/patches/"${patch}"
  done

  sed '20 aAM_PROG_AR' -i $srcdir/$_pkgbasename-$pkgver/configure.ac

  autoreconf -vif
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}-${pkgver}

  # configure options
  # --enable-svg
  # --enable-ps
  # --enable-pdf
  # --enable-xlib-xcb
  # --enable-gobject

  ./configure --prefix=/usr \
    --libdir=/usr/lib32 \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --disable-lto \
    --enable-tee \
    --enable-gl \
    --enable-egl
  make
}

package() {
  cd ${_pkgbasename}-${pkgver}
  make DESTDIR=$pkgdir install
  rm -rf ${pkgdir}/usr/{bin,include,share}
}
